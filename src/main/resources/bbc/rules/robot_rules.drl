package bbc

import bbc.*;
import robocode.*;
import robocode.ScannedRobotEvent;
import java.util.HashMap;
import java.awt.geom.Point2D;
import java.util.Set;

global HashMap<String,GravitationalPoint> EnemyPos;

//global Map<String,EnemyRobot>mapTest = new HashMap<String,EnemyRobot>();

query "consult_actions"
  action: Action()
end



//
////// RULES
//

declare Target
	name: String
end




// Storing Victim's informations
declare Victim
   name: String
   x: double
   y: double
   distance: double
   bearing: double
   heading: double
   velocity: double
   energy: double
   time: long
   firePower: double
   bulletSpeed: double
end

// Storing Robot status informations
declare Status
	energy: double
	others: int
	x: double
	y: double
	distanceRemaining: double
	gunHeading: double
	gunHeat: double
	gunTurnRemaining: double
	heading: double
	numRounds: int
	radarHeading: double
	radarTurnRemaining: double
	roundNum: int
	time: long
	turnRemaining: double
	velocity: double
	scanDirection: int
	moveDirection: int
	found : int
end




// ========== CLEANING DOUBLE RULES ==========

rule "WHEN old action THEN clean it"
	salience 120
	when
		$action: Action($time: time)
		$state : RobotState()
		eval($time < $state.getTime())
	then
	    retract($action);
	    System.out.println("Rule from last turn cleaned.");
end


// ========== STATUS RULES ==========


rule "WHEN a victim dies retract victim"
	salience 119
	when
		$dead: RobotDeathEvent()
		$status: Status()
		$victim: Victim(name==$dead.getName())
	then
		
		if (EnemyPos.containsKey($dead.getName()) == true) EnemyPos.remove($dead.getName());
	    $status.setFound($status.getFound()-1);
		retract($victim);
end

rule "WHEN target dies, retract target"
	salience 118
	when
		
		$target: Target()
		not (exists Victim(name == $target.getName() ) )
	then
		retract($target);
end






rule "WHEN no status THEN create one"
	salience 100
	when
		not(exists Status())
		$status: RobotState()
	then
		Status status = new Status();
		status.setMoveDirection(1);
		status.setScanDirection(1);
		status.setFound(0);
	    insert(status);
	    System.out.println("Internal status created.");
end

rule "WHEN new turn THEN update status"
	salience 99
	when
		$status: Status()
		$state: RobotState()
	then
		$status.setEnergy($state.getEnergy());
	    $status.setOthers($state.getOthers());
	    $status.setX($state.getX());
	    $status.setY($state.getY());
	   	$status.setDistanceRemaining($state.getDistanceRemaining());
		$status.setGunHeading($state.getGunHeading());
		$status.setGunHeat($state.getGunHeat());
		$status.setGunTurnRemaining($state.getGunTurnRemaining());
		$status.setHeading($state.getHeading());
		$status.setNumRounds($state.getNumRounds());
		$status.setRadarHeading($state.getRadarHeading());
		$status.setRadarTurnRemaining($state.getRadarTurnRemaining());
		$status.setRoundNum($state.getRoundNum());
		$status.setTime($state.getTime());
		$status.setTurnRemaining($state.getTurnRemaining());
		$status.setVelocity($state.getVelocity());
	    System.out.println("Internal status updated.");
	    System.out.println("#Turn : " + $status.getTime());
end


// ========== VICTIM SET & UPDATE RULES ==========

rule "WHEN found a new victim, then update it."
	salience 89
	when
		$scanned: ScannedRobotEvent()
		$status : Status()
		$state: RobotState()
		not (exists Victim(name == $scanned.getName()))
	then
	    $status.setFound($status.getFound()+1);
	    Victim victim = new Victim();
		victim.setName($scanned.getName());
		victim.setDistance($scanned.getDistance());
		victim.setBearing($scanned.getBearing());
	    victim.setHeading($scanned.getHeading());
	    victim.setVelocity($scanned.getVelocity());
	    victim.setEnergy($scanned.getEnergy());
		double enemyX = Helper.enemyX($status.getX(), $status.getHeading(), $scanned.getBearing(), $scanned.getDistance());
		double enemyY = Helper.enemyY($status.getY(), $status.getHeading(), $scanned.getBearing(), $scanned.getDistance());
		victim.setX(enemyX);
		victim.setY(enemyY);
		double firePower = Helper.firePower($scanned.getDistance());
		victim.setFirePower(firePower);
		double bulletSpeed = Helper.bulletSpeed(firePower);
		victim.setBulletSpeed(bulletSpeed);
		long time = Helper.time($scanned.getDistance(), bulletSpeed);
		victim.setTime(time);
		insert(victim);
		
		GravitationalPoint p = new GravitationalPoint(enemyX,enemyY,$scanned.getEnergy());
		
		EnemyPos.put($scanned.getName(),p);
		
  		System.out.println("Victim created."+$scanned.getName());
	    System.out.println("Planned bullet : Power = "+ firePower +" Speed = "+ bulletSpeed +" Time = "+ time);
end

rule "WHEN new turn and detected old victim THEN update victim's informations"
	salience 88
	when
		$scanned : ScannedRobotEvent()
		$status : Status()
		$state : RobotState()
		//eval($victim.getName() == $scanned.getName())
		$victim : Victim(name == $scanned.getName())
	then
		$victim.setDistance($scanned.getDistance());
		$victim.setBearing($scanned.getBearing());
	    $victim.setHeading($scanned.getHeading());
	    $victim.setVelocity($scanned.getVelocity());
	    $victim.setEnergy($scanned.getEnergy());
		double enemyX = Helper.enemyX($status.getX(), $status.getHeading(), $scanned.getBearing(), $scanned.getDistance());
		double enemyY = Helper.enemyY($status.getY(), $status.getHeading(), $scanned.getBearing(), $scanned.getDistance());
		$victim.setX(enemyX);
		$victim.setY(enemyY);
		double firePower = Helper.firePower($scanned.getDistance());
		$victim.setFirePower(firePower);
		double bulletSpeed = Helper.bulletSpeed(firePower);
		$victim.setBulletSpeed(bulletSpeed);
		long time = Helper.time($scanned.getDistance(), bulletSpeed);
		$victim.setTime($status.getTime());
		
		GravitationalPoint p = new GravitationalPoint(enemyX,enemyY,$scanned.getEnergy());
		
		EnemyPos.put($scanned.getName(),p);
		
		//update($victim);
	    System.out.println("Updated victim informations."+$scanned.getName());
end

rule "WHEN new turn and detected new victim create new victim"
	salience 88
	when
		$scanned : ScannedRobotEvent()
		$status : Status()
		$state : RobotState()
		not (exists Target())
	then
		Target target = new Target();
		target.setName($scanned.getName());
		insert(target);
  		System.out.println("Victim target = "+$scanned.getName());
end



//Target(name==$victim.getName())
rule "WHEN detected closer victim than old one THEN update to new victim"
	salience 87
   	when
   		$target : Target()
		$victim1 : Victim(name == $target.getName())
		$victim2 : Victim(name != $target.getName())
      	$status : Status()
      	$state : RobotState()
		eval($victim1.getDistance() > $victim2.getDistance())
   	then
   		$target.setName($victim2.getName());
   		update($target);
end
/*
rule "WHEN detected closer victim than old one THEN update to new victim"
	salience 87
   	when
		$victim : Victim()
      	$scanned: ScannedRobotEvent()
      	$status : Status()
      	$state : RobotState()
		eval($victim.getDistance() > $scanned.getDistance())
   	then
		$victim.setName($scanned.getName());
		$victim.setDistance($scanned.getDistance());
		$victim.setBearing($scanned.getBearing());
	    $victim.setHeading($scanned.getHeading());
	    $victim.setVelocity($scanned.getVelocity());
	    $victim.setEnergy($scanned.getEnergy());
		double enemyX = Helper.enemyX($status.getX(), $status.getHeading(), $scanned.getBearing(), $scanned.getDistance());
		double enemyY = Helper.enemyY($status.getY(), $status.getHeading(), $scanned.getBearing(), $scanned.getDistance());
		$victim.setX(enemyX);
		$victim.setY(enemyY);
		double firePower = Helper.firePower($scanned.getDistance());
		$victim.setFirePower(firePower);
		double bulletSpeed = Helper.bulletSpeed(firePower);
		$victim.setBulletSpeed(bulletSpeed);
		long time = Helper.time($scanned.getDistance(), bulletSpeed);
		$victim.setTime(time);
		
		GravitationalPoint p = new GravitationalPoint(enemyX,enemyY,$scanned.getEnergy());
		
		EnemyPos.put($scanned.getName(),p);
		
	    System.out.println("Closer victim detected and targeted");
end
*/

// ========== RADAR TURNING RULES ==========

rule "WHEN there is a robot that is hasnt been scaned in a while"
	salience 81
	when
		//not (exists ScannedRobotEvent())
		$state : RobotState()
		$status : Status()
		$victim : Victim(time<($state.getTime()-16) ) 
	then
		//double angle = Helper.relativeAngle($victim.getBearing() + $state.getHeading() , $state.getRadarHeading());
		//double angle = Helper.relativeAngle($victim.getBearing() + $state.getHeading() , $state.getRadarHeading());
		//$status.setScanDirection(Helper.sign(angle));

		insert(new Action(Action.RADAR_RIGHT, 360*$status.getScanDirection(), 1, $state.getTime()));
		//retract($victim);
		//$status.SetFound($status.getFound()-1);
		//update($status);
		System.out.println("Removed old victim and set direction to:" +  $status.getScanDirection());
end

rule "WHEN missing robot then complete turn"
	salience 80
	when
		//not (exists ScannedRobotEvent())
		$state : RobotState()
		$status : Status()
		eval($status.getFound() < $state.getOthers())  
	then
		insert(new Action(Action.RADAR_RIGHT, 360* $status.getScanDirection(), 1, $state.getTime()));
		
		System.out.println("Radar complete turn initialized"+ ($status.getFound() < $state.getOthers()) );
end

rule "WHEN scanned victim THEN oscillate radar on it"
	salience 79
	when
		$scanned : ScannedRobotEvent()
		$status : Status()
		$state : RobotState()
		Target(name==$scanned.getName())
		not (eval($status.getFound() < $state.getOthers()) or (exists Victim(time<($state.getTime()-16) )) ) 
	then
		$status.setScanDirection(-1 * $status.getScanDirection());
		insert(new Action(Action.RADAR_RIGHT, 360 * $status.getScanDirection(), 1, $state.getTime()));
	    System.out.println("Oscillate on :"+ $scanned.getName());
end


// ========== AIMING RULES ==========

rule "WHEN exists victim THEN set gun to predicted position"
	salience 69
	when
		$victim : Victim()
		$status : Status()
		$state : RobotState()
		Target(name==$victim.getName())
	then
		long time = Helper.time(Helper.distance($state.getX(), $state.getY(), $victim.getX(), $victim.getY()), $victim.getBulletSpeed());
		double futureX = Helper.enemyFutureX($victim.getX(), $victim.getHeading(), $victim.getVelocity(), time);
		double futureY = Helper.enemyFutureY($victim.getY(), $victim.getHeading(), $victim.getVelocity(), time);
		double absDeg = Helper.absoluteBearing($state.getX(), $state.getY(), futureX, futureY);
		double finalAngle = Helper.normalizeBearing(absDeg - $state.getGunHeading());
		insert(new Action(Action.GUN_RIGHT, finalAngle, 1, $state.getTime()));
	    System.out.println("Victim's next position predicted, aiming.....");
end

// ========== SHOOTING RULES ==========

rule "WHEN gun is cool and on target THEN FIRE"
	salience 68
	when
		$target: Target()
		$state : RobotState()
		eval($state.getGunHeat() == 0.0)
		eval($state.getGunTurnRemaining() < 10)
		$victim : Victim (name == $target.name)
	then
		insert(new Action(Action.SHOOT, $victim.getFirePower(), 1, $state.getTime()));
	    System.out.println("AND ..... FIIIIIIRRREEEEEEE");
end

// ========== MOVING RULES ==========

//Função para a movimentação

rule "Robot moves according with gravitational rules"
	salience 60
	when
		$state : RobotState()
		$status : Status()
		$battlestate: BattleState()
		eval($status.getTime() % 11 == 0)
	then
		
		double xforce = 0;
    	double yforce = 0;
    	double dist = 300;
    	double force = 0;
    	double ang = 0;
    	
    	double posx = 0;
    	double posy = 0;
	    
	    if (!(EnemyPos.isEmpty())) {
		   	Set<String> x = EnemyPos.keySet();
		   	
		   	for (String s : x) {
		   		GravitationalPoint p = (GravitationalPoint)EnemyPos.get(s);
		   		force = p.power/Math.pow(Helper.distance($status.getX(),$status.getY(),p.x,p.y),2);
	        	ang = Helper.normalizeBearing(Math.PI/2 - Math.atan2($status.getY() - p.y, $status.getX() - p.x)); 
	        	xforce += Math.sin(ang) * force;
	        	yforce += Math.cos(ang) * force;
		   	}
		}
	    	
	   	xforce += 5000/Math.pow(Helper.distance($state.getX(), $state.getY(), $battlestate.getFieldWidth(), $state.getY()), 3);
	   	xforce -= 5000/Math.pow(Helper.distance($state.getX(), $state.getY(), 0, $state.getY()), 3);
	   	yforce += 5000/Math.pow(Helper.distance($state.getX(), $state.getY(), $state.getX(), $battlestate.getFieldHeight()), 3);
	   	yforce -= 5000/Math.pow(Helper.distance($state.getX(), $state.getY(), $state.getX(), 0), 3);
		
		posx = $state.getX() - xforce;
		posy = $state.getY() - yforce;
		
		double angle = Math.toDegrees(Helper.absoluteAngle($state.getX(),$state.getY(),posx,posy));
		angle = Helper.RobotAngleForGravitationalPoints ($state.getHeading(), angle);
		
		double r = Helper.DirectionforTheRobot (angle);
		angle = Helper.RobotAngleModifiedForGravitationalPoints (angle);
		
		insert(new Action(Action.TANK_LEFT, angle, 1, $state.getTime()+7));
		insert(new Action(Action.AHEAD, dist*r, 1, $state.getTime()+7));
		
		System.out.println("Moving initialized");

end